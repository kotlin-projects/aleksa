group 'de.mkammerer.aleksa'
version '1.2-SNAPSHOT'

description 'Aleksa is a small framework for writing Alexa Skills in Kotlin'

buildscript {
    ext.kotlin_version = '1.2.71'
    ext.jetty_version = '9.4.12.v20180830'
    ext.dokka_version = '0.9.16'
    ext.alexa_kit_version = '1.8.1'
    ext.commons_cli_version = '1.4'
    ext.slf4j_version = '1.7.25'
    ext.junit_version = '4.12'
    ext.logback_version = '1.2.3'
    ext.metrics_version = '4.0.3'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

apply plugin: 'kotlin'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: jetty_version
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: jetty_version
    compile(group: 'com.amazon.alexa', name: 'alexa-skills-kit', version: alexa_kit_version) {
        exclude module: 'log4j'
        exclude module: 'slf4j-log4j12'
    }
    compile group: 'commons-cli', name: 'commons-cli', version: commons_cli_version
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
    compile group: 'io.dropwizard.metrics', name: 'metrics-core', version: metrics_version
    compile group: 'io.dropwizard.metrics', name: 'metrics-servlets', version: metrics_version

    testCompile group: 'junit', name: 'junit', version: junit_version
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: logback_version
}

repositories {
    mavenCentral()
}

signing {
    sign configurations.archives
    required { gradle.taskGraph.hasTask("uploadArchives") }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: project.properties['sonatype.username'], password: project.properties['sonatype.password'])
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: project.properties['sonatype.username'], password: project.properties['sonatype.password'])
            }
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            enhancePom(pom)
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.kotlin
}

task javadocJar(type: Jar, dependsOn: 'dokkaJavadoc') {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

task dokkaJar(type: Jar, dependsOn: 'dokka') {
    classifier = 'dokka'
    from "$buildDir/dokka"
}


artifacts {
    archives sourcesJar
    archives javadocJar
    archives dokkaJar
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

/**
 * Adds license, developer and SCM tags to the POM file.
 * @param pom POM.
 */
void enhancePom(MavenPom pom) {
    pom.project {
        url 'https://github.com/phxql/aleksa'
        name rootProject.name
        description project.description
        licenses {
            license {
                name 'GNU Lesser General Public License, Version 3'
                url 'https://www.gnu.org/licenses/lgpl-3.0.en.html'
            }
        }
        developers {
            developer {
                name 'Moritz Kammerer'
                email 'kammerer.moritz@gmail.com'
                url 'https://www.mkammerer.de/'
            }
        }
        scm {
            connection 'scm:git:git://github.com/phxql/aleksa.git'
            developerConnection 'scm:git:git://github.com/phxql/aleksa.git'
            url 'https://github.com/phxql/aleksa'
        }
    }
}